\documentclass[11pt]{report}

% Socument dimensions
\usepackage{geometry}
\geometry{top=1.5cm, bottom=1.5cm, textwidth=15cm}

% Math related packges.
\usepackage{amsmath}
\usepackage{cancel}

% Natural Seduction package
\usepackage{proof}
\usepackage{mdframed}

% Fix the header space: start at the top of the page.
\usepackage{hyperref}

% Import the necessary preamble for the document. 
\usepackage{../../../proofsPrograms}


\begin{document}
	
	
% Heading for the tutorial	
\begin{center}
	{\bf MATH230: Tutorial Nine (Solutions)}
\end{center}
\begin{center}
	{\bf Proofs-as-Programs: Minimal Logic in L$\exists\forall$N}
\end{center}


% Box with goals and relevent lecture notes.
\noindent\fbox{
	\parbox{\textwidth}{

		Key ideas
			\begin{itemize}
				\item Learn L$\exists\forall$N syntax.
				\item Write proof terms in L$\exists\forall$N.
				\item Compare syntax across languages. 
			\end{itemize}

		Relevant lectures: \\
		Relevant reading: \href{https://lean-lang.org/theorem_proving_in_lean4/propositions_and_proofs.html}{Theorem Proving in L$\exists\forall$N 4} 
		
	\vspace{0.2cm}

	Hand in exercises: 1 \\ 
	{\bf Sue Friday @ 5pm to the tutor, or to lecturer.}
	}
}
% Discussion questions for tutor.
\newline
\vspace{0.5cm}

\noindent {\bf Discussion Questions}

\begin{enumerate}

	\item In a previous tutorial we wrote a proof-term witnessing the sequent $$ P \rightarrow Q \ \vdash \ P \rightarrow ( P \land  Q)$$
	Translate this proof-term into the syntax for L$\exists\forall$N 4.
	% These discussions should be based around the typed lambda terms they wrote in previous tutorials. This tutorial is mainly about translating into LEAN and not about reproving all these theorems.  

	\vspace{3cm}

	\item Write a proof-term in L$\exists\forall$N 4 to prove the following sequent $$ P\vdash \lnot \lnot  P$$
	 

	\vspace{3cm}

	\item Write a proof-term in L$\exists\forall$N 4 to prove the following sequent $$( P\land  Q) \lor  R \ \vdash \ ( P\lor  R) \land ( Q \lor  R)$$
	

\end{enumerate}

% New page for tutorial exercises.
\newpage
{\bf Tutorial Exercises}

\begin{enumerate}
	
	\item Throughout the course we have introduced rules of inference in logic, type constructors and destructors in the typed $\lambda$-calculus, and now we are to see how they are implemented in L$\exists\forall$N. As you write proof-terms for the sequents in Exercise 2 enter the missing components in the following table: 
	
	\begin{table}[htbp]
		\centering
		\begin{tabular}{p{2cm} p{2cm} p{2cm}}
			\textbf{PL} & $\boldsymbol{\lambda}$ & \textbf{L$\exists\forall$N 4} \\
			\hline
			$\land I$& & \\ 
			$\land E_{r}$& & \\
			$\land E_{l}$& & \\
			$\to I$& $\lambda$\_ : \_ . & $\lambda \_ : \_ =>$\\
			$\to E$& & \\
			$\lor I_{r}$& & \\
			$\lor I_{l}$& & \\
			$\lor E$& & 
		\end{tabular}
		\caption{Syntax of logic, $\lambda$-calculus, and L$\exists\forall$N 4}
		\label{tab:your_table_label}
	\end{table}

	% \begin{table}[htbp]
	% 	\centering
	% 	\begin{tabular}{p{2cm} p{2cm} p{2cm}}
	% 		%\hline
	% 		\textbf{PL} & $\boldsymbol{\lambda}$ & \textbf{L$\exists\forall$N 4} \\
	% 		\hline
	% 		$\land I$& $\times$ & And.intro \\ 
	% 		$\land E_{l}$& \FST & And.left \\
	% 		$\land E_{r}$& \SNS & And.right \\
	% 		$\to I$& $\lambda$ & $\lambda \_ : \_ =>$\\
	% 		$\to E$& \APP & \\
	% 		$\lor I_{l}$& \inl & Or.intro\_left \\
	% 		$\lor I_{r}$& \inr & Or.intro\_right \\
	% 		$\lor E$& \sumElim & Or.elim
	% 	\end{tabular}
	% 	\caption{Syntax of logic, $\lambda$-calculus, and L$\exists\forall$N 4}
	% 	\label{tab:your_table_label}
	% \end{table}
	
	\item For each of the sequents below, write proof-terms in L$\exists\forall$N 4. You have seen these proofs before, so it is a translating into L$\exists\forall$N exercise, rather than proving anything new. 
	 
	\begin{enumerate}

		\item $P \to Q, \lnot Q \ \vdash \ \lnot P$
		
			\begin{lstlisting}
			--                         P → Q,       ¬Q  ⊢ ¬P
			theorem modus_tollens (f : P → Q) (nq : ¬Q) : ¬P :=
			λ wp : P =>
				nq (f wp)
			\end{lstlisting}

		\item $( P\land  Q) \rightarrow  R \dashv\vdash  P\rightarrow ( Q \rightarrow  R) $
		
		This proof has been written in both directions separately. These are combined into a proof of the iff at the end. Notice the comments, indicated by lines with \texttt{---} at the beginning, show how the sequent syntax that we have been using throughout the course lines up with the syntax of L$\exists\forall$N. 
		
			\begin{lstlisting}
			--                 (P ∧ Q) → R  ⊢ P → (Q → R)
			theorem curry (f : (P ∧ Q) → R) : P → (Q → R) :=
			λ wp : P =>
				λ wq : Q =>
				f (And.intro wp wq)
			
			--                   P → (Q → R)  ⊢ (P ∧ Q) → R
			theorem uncurry (f : P → (Q → R)) : (P ∧ Q) → R :=
			λ wpq : P ∧ Q =>
				(f wpq.left) (wpq.right)
			
			--                 P → (Q → R) $\dashv$⊢ (P ∧ Q) → R
			theorem currying : P → (Q → R) ↔ (P ∧ Q) → R :=
			Iff.intro
				(λ f : P → (Q → R) =>
					λ wpq : P ∧ Q =>
					(f wpq.left) (wpq.right))
				(λ f : (P ∧ Q) → R =>
				λ wp : P =>
					λ wq : Q =>
					f (And.intro wp wq))
			\end{lstlisting}

		Proofs are programs and therefore can be factored like any other program! Since the proofs we write are programs, they can be called upon in later proofs. This means that the proof of the iff can be simplified by calling the proofs of each direction. 

			\begin{lstlisting}
			--                P → (Q → R) $\dashv$⊢ (P ∧ Q) → R
			theorem currying : P → (Q → R)  ↔ (P ∧ Q) → R :=
			Iff.intro
				(uncurry P Q R)
				(curry P Q R)
			\end{lstlisting}

		\newpage
		\item $\lnot P\lor \lnot Q \vdash \lnot( P\land  Q)$
		
			\begin{lstlisting}
			--                                   ¬P ∨ ¬Q  ⊢ ¬(P ∧ Q)
			theorem deMogran_notConj_factor (t : ¬P ∨ ¬Q) : ¬(P ∧ Q) :=
			λ w : P ∧ Q =>
				Or.elim t
				(λ np : ¬P =>
					np w.left)
				(λ nq : ¬Q =>
					nq w.right)
			\end{lstlisting}

		\newpage
		\item $\lnot( P\lor  Q) \dashv\vdash \lnot  P\land \lnot  Q$
		
		\begin{lstlisting}
		--                     ¬(P ∨ Q) ⊢ ¬P ∧ ¬Q
		--                              ⊢ ¬(P ∨ Q) → ¬P ∧ ¬Q
		theorem deMorgan_notDisj_expand : ¬(P ∨ Q) → ¬P ∧ ¬Q :=
		λ f : ¬(P ∨ Q) =>
			And.intro (λ wp : P =>
						f (Or.intro_left Q wp))
					(λ wq : Q =>
						f (Or.intro_right P wq))
		
		--                      ¬P ∧ ¬Q ⊢ ¬(P ∨ Q)
		--                              ⊢ ¬P ∧ ¬Q → ¬(P ∨ Q)
		theorem deMorgan_notDisj_factor : ¬P ∧ ¬Q → ¬(P ∨ Q) :=
		λ t1 : ¬P ∧ ¬Q =>
			λ t2 : P ∨ Q =>
			Or.elim t2
				(λ wp : P =>
				t1.left wp)
				(λ wq : Q =>
				t1.right wq)
		\end{lstlisting}

		\item $ P\rightarrow  Q, \  Q \rightarrow  R \vdash  P\rightarrow  R $

		\begin{lstlisting}
		--                            P → Q,      Q → R  ⊢ P → R
		theorem impl_composition (f : P → Q) (g : Q → R) : P → R :=
		λ wp : P =>
			g (f wp)
		\end{lstlisting}

		\item $ P\lor  Q,\ \  P\to  R,\ \  Q \to  S \vdash   R \lor  S$
		
			\begin{lstlisting}
			theorem case (t : P ∨ Q) (f : P → R) (g : Q → S) : R ∨ S :=
			Or.elim t
			(λ wp : P =>
				Or.intro_left S (f wp))
			(λ wq : Q =>
				Or.intro_right R (g wq))
			\end{lstlisting}

		\item $ P\to  R,\ \  Q \to  S,\ \ \neg R \lor \neg  S \vdash  \neg P\lor \neg  Q$
		
			\begin{lstlisting}
			--            ¬R ∨ ¬S,      P → R,      Q → S  ⊢ ¬P ∨ ¬Q
			example (t1 : ¬R ∨ ¬S) (f : P → R) (g : Q → S) : ¬P ∨ ¬Q :=
			Or.elim t1
				(λ nr : ¬R =>
				Or.intro_left (¬Q)
					(λ wp : P =>
					nr (f wp)))
				(λ ns : ¬S =>
				Or.intro_right (¬P)
					(λ wq : Q =>
					ns (g wq)))
			\end{lstlisting}

		\item $ P,\ \ \neg  P\vdash  \neg  Q$
		
			\begin{lstlisting}
			--            ¬R ∨ ¬S,      P → R,      Q → S  ⊢ ¬P ∨ ¬Q
			example (t1 : ¬R ∨ ¬S) (f : P → R) (g : Q → S) : ¬P ∨ ¬Q :=
			Or.elim t1
				(λ nr : ¬R =>
				Or.intro_left (¬Q)
					(λ wp : P =>
					nr (f wp)))
				(λ ns : ¬S =>
				Or.intro_right (¬P)
					(λ wq : Q =>
					ns (g wq)))
			\end{lstlisting}

		Alternatively, we can factor out the proof of Modus Tollens from above. Each of these theorems you are proving are programs. This means they can be called in other proofs - just as we have been recycling proofs in our natural deductions. Here is the same proof, but with Modus Tollens factored out. 

			\begin{lstlisting}			
			--            ¬R ∨ ¬S,      P → R,      Q → S  ⊢ ¬P ∨ ¬Q
			example (t1 : ¬R ∨ ¬S) (f : P → R) (g : Q → S) : ¬P ∨ ¬Q :=
			Or.elim t1
				(λ nr : ¬R =>
				Or.intro_left (¬Q)
								(modus_tollens P R f nr))
				(λ ns : ¬S =>
				Or.intro_right (¬P)
								(modus_tollens Q S g ns))
			\end{lstlisting}

	   \item $ P\rightarrow Q, \  P\rightarrow \lnot Q \vdash \lnot  P$
	   
		\begin{lstlisting}
		--                        P → Q       P → ¬Q  ⊢ ¬P
		theorem lesser_falso (f : P → Q) (g : P → ¬Q) : ¬P :=
		λ wp : P =>
			(g wp) (f wp)
		\end{lstlisting}

	\end{enumerate}

	\item For each of the sequents below write proof-terms in L$\exists\forall$N. These exercises have not appeared in earlier tutorials. You could prove these by-hand first and then translate that into L$\exists\forall$N. Or you could write the proof straight into L$\exists\forall$N.
	
		\begin{enumerate}
			\item $P \vdash \lnot\lnot P$
			
			\begin{lstlisting}
			--                             P  ⊢ ¬¬P
			theorem double_neg_intro (wp : P) : ¬¬P :=
			λ wnp : ¬P =>
				wnp wp
			\end{lstlisting}

			\item $\lnot\lnot\lnot P \vdash \lnot P$
			
			\begin{lstlisting}
			--                       ¬¬¬P  ⊢ ¬P
			theorem minmial_dne (f : ¬¬¬P) : ¬P :=
			λ wp : P =>
				f (double_neg_intro P wp)
			\end{lstlisting}

			\item $ \vdash \lnot \lnot (P \lor \lnot P)$
			
			\begin{lstlisting}
			--                      ⊢ ¬¬(P ∨ ¬P)
			theorem cant_refute_lem : ¬¬(P ∨ ¬P) :=
				λ g : ¬(P ∨ ¬P) =>
				g (Or.intro_right (P) 
								(λ x : P =>
									g (Or.intro_left (¬P) x))) 
			\end{lstlisting}
		\end{enumerate}

\end{enumerate}
	
\end{document}